file('build.properties').withReader {
    def prop = new Properties()
    prop.load it
    project.ext.config = new ConfigSlurper().parse prop
}

buildscript {
    repositories {
        maven {
            url "https://maven.eveoh.nl/content/repositories/releases"
        }
    }
    dependencies {
        classpath "nl.eveoh:gradle-aspectj:1.5"
    }
}

project.ext.aspectjVersion = config.version_aspectj

apply plugin: 'java'
apply plugin: 'aspectj'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

version = config.version
group = config.group

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile([group: 'org.apache.commons', name: 'commons-lang3', version: config.version_commons_lang])
}

jar {
    manifest {
        if (!config.mainClass.isEmpty()) attributes "Main-Class": config.mainClass
    }
}

task testJar(type: Jar, dependsOn: javadoc) {
    from sourceSets.main.output
    from sourceSets.test.output
    classifier = 'test'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier = 'javadoc'
}

build.dependsOn testJar, sourceJar, javadocJar